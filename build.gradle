apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'

group = 'be.thinkerit.gradle'
version = System.getenv('VERSION') ?: 'dev-SNAPSHOT'
sourceCompatibility = 1.6

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

dependencies {
    groovy 'org.codehaus.groovy:groovy:1.8.4'

    compile gradleApi()
    compile 'joda-time:joda-time:1.6.2'

    runtime 'commons-lang:commons-lang:2.6'

    testCompile 'junit:junit:4.8.2'
    testCompile 'ch.qos.logback:logback-classic:1.0.0'
    testCompile "org.spockframework:spock-core:0.6-groovy-1.8", {
        exclude module:"groovy-all"
    }

    deployerJars "org.apache.maven.wagon:wagon-http:1.0"
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives packageJavadoc
    archives packageSources
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars

        pom.project {
            artifactId 'ubuntu-packager-plugin'
            name 'ubuntu-packager-plugin - Gradle plugin for constructing Ubuntu/Debian packages'
            packaging 'jar'
            description 'Provides a task similar to Tar and Zip for constructing debian package files.'
            inceptionYear '2011'

            developers {
                developer {
                    id 'sgo'
                    name 'Sebastian Gozin'
                    email 'sgo@thinkerit.be'
                    roles {
                        role 'Developer'
                    }
                    timezone '+1'
                }
            }
        }
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-9'
}
